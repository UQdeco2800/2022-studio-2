buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {}
}

plugins {
    id "org.sonarqube" version "3.2.0"
    id "jacoco"
}

sonarqube {
    properties {
        property "sonar.projectKey", "UQdeco2800_game-engine"
        property "sonar.organization", "uqdeco2800-test"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: 'jacoco'

    version = '1.0'
    ext {
        appName = "deco2800-game"
        gdxVersion = '1.9.14'
        roboVMVersion = '2.3.12'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.3'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.1.0'
        junit5Version = '5.7.2'
        junit4Version = '4.13.2'
        mockitoVersion = '3.8.0'
        gdxUtilsVersion = '0.13.4'
        slf4jVersion = '1.7.30'
    }

    repositories {
        mavenCentral()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java-library"

    // Enforce use of JDK 11. Will download if it isn't in default OS location.
//    java {
//        toolchain {
//            languageVersion.set(JavaLanguageVersion.of(11))
//        }
//    }

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        api "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
    }
}

project(":core") {
    apply plugin: "java-library"

    // Enforce use of JDK 11. Will download if it isn't in default OS location.
//    java {
//        toolchain {
//            languageVersion.set(JavaLanguageVersion.of(11))
//        }
//    }

    dependencies {
        // libGDX
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        api "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
        api "net.dermetfan.libgdx-utils:libgdx-utils:$gdxUtilsVersion"
        api "net.dermetfan.libgdx-utils:libgdx-utils-box2d:$gdxUtilsVersion"

        // Logging
        api "org.slf4j:slf4j-api:$slf4jVersion"
        api "org.slf4j:slf4j-jdk14:$slf4jVersion"

        // Unit Testing
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junit5Version"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit5Version"

        testImplementation("junit:junit:$junit4Version")
        testRuntimeOnly("org.junit.vintage:junit-vintage-engine") {
            because 'Allows JUnit 3 and JUnit 4 tests to run'
        }
        testRuntimeOnly("org.junit.platform:junit-platform-launcher") {
            because 'Allows tests to run from IDEs that bundle older version of launcher'
        }

        testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
        testImplementation "org.mockito:mockito-inline:$mockitoVersion"

        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testImplementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    }

    test {
        useJUnitPlatform {}

        testLogging {
            events 'passed', 'skipped', 'failed'
        }

        finalizedBy(jacocoTestReport)
    }
}
